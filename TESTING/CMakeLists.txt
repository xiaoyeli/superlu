include_directories(${SuperLU_SOURCE_DIR}/SRC)

# Libs linked to all of the tests
set(test_link_libs matgen)

add_subdirectory(MATGEN)

set(NVAL   9 19)
set(NRHS   2)
set(LWORK  0 10000000)

function(add_superlu_test target input)
  set(TEST_INPUT "${SuperLU_SOURCE_DIR}/EXAMPLE/${input}")

  foreach (s ${NRHS})
    foreach(l ${LWORK})
      # LA tests
      foreach (n ${NVAL})
        set(testName "${target}_${n}_${s}_${l}")
        add_test(
          NAME ${testName}_LA
          COMMAND ${target} -t "LA" -n ${n} -s ${s} -l ${l})
      endforeach()

      # SP tests
      set(testName "${target}_${s}_${l}")
      add_test(
        NAME ${testName}_SP
        COMMAND ${target} -t "SP" -s ${s} -l ${l} -f "${TEST_INPUT}")
    endforeach()
  endforeach()

endfunction(add_superlu_test)


if(enable_single)
  set(SLINTST sdrive.c sp_ienv.c sp_sconvert.c sgst01.c sgst02.c sgst04.c sgst07.c)

  add_executable(s_test ${SLINTST})
  target_link_libraries(s_test ${test_link_libs})

  add_superlu_test(s_test g20.rua)
endif()

if(enable_double)
  set(DLINTST ddrive.c sp_ienv.c sp_dconvert.c dgst01.c dgst02.c dgst04.c dgst07.c)

  add_executable(d_test ${DLINTST})
  target_link_libraries(d_test ${test_link_libs})
  
  if(MSVC)
    target_include_directories(d_test PRIVATE ${WinGetOpt_INCLUDE_DIR})
    target_link_libraries(d_test ${WinGetOpt_LIBRARY})
  endif()

  add_superlu_test(d_test g20.rua)
endif()

if(enable_complex)
  set(CLINTST cdrive.c sp_ienv.c sp_cconvert.c cgst01.c cgst02.c cgst04.c cgst07.c)

  add_executable(c_test ${CLINTST})
  target_link_libraries(c_test ${test_link_libs})
  
  if(MSVC)
    target_include_directories(c_test PRIVATE ${WinGetOpt_INCLUDE_DIR})
    target_link_libraries(c_test ${WinGetOpt_LIBRARY})
  endif()

  add_superlu_test(c_test cg20.cua)
endif()


if(enable_complex16)
  set(ZLINTST zdrive.c sp_ienv.c sp_zconvert.c zgst01.c zgst02.c zgst04.c zgst07.c)

  add_executable(z_test ${ZLINTST})
  target_link_libraries(z_test ${test_link_libs})
  
  if(MSVC)
    target_include_directories(z_test PRIVATE ${WinGetOpt_INCLUDE_DIR})
    target_link_libraries(z_test ${WinGetOpt_LIBRARY})
  endif()

  add_superlu_test(z_test cg20.cua)
endif()
